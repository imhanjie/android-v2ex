ext {
    compileSdkVersion = 30
    minSdkVersion = 21
    targetSdkVersion = 30

    junit = "4.13.2"

    activity = "1.2.0"
    appcompat = "1.2.0"
    fragment = "1.3.0"
    core_ktx = "1.3.2"
    multidex = "2.0.1"
    kotlin = "1.4.21"
    kotlin_coroutines = '1.3.7'
    lifecycle = "2.3.0"
    work_manager = "2.5.0"
    retrofit = "2.9.0"
    okhttp = "3.14.7"
    paging = "2.1.2"
    room = "2.2.6"
    stetho = "1.5.1"
    glide = "4.11.0"
    big_image_view_pager = "androidx-6.0.1"
    hilt = "2.28-alpha"

    android_utils = "0.2.2"
    v2ex_api = "0.2.39"
}

/**
 * 统一依赖版本
 */
def handleDependency(ConfigurationContainer configurations) {
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                String group = details.requested.group
                String module = details.requested.name
                if (group == 'androidx.lifecycle' && module.startsWith("lifecycle-")) {
                    details.useVersion rootProject.ext.lifecycle
                }
                if (group == 'androidx.activity' && module == 'activity') {
                    details.useVersion rootProject.ext.activity
                }
                if (group == 'androidx.fragment' && module == 'fragment') {
                    details.useVersion rootProject.ext.fragment
                }
            }
        }
    }
}


static def addRepository(RepositoryHandler handler) {
    handler.google()
    handler.jcenter()
    handler.maven { url 'https://jitpack.io' }
}

ext.addRepository = this.&addRepository
ext.handleDependency = this.&handleDependency